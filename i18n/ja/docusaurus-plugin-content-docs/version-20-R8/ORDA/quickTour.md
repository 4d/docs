---
id: quickTour
title: ORDA の概要
---

ORDA はオブジェクトベースであるため、ORDA を使うにはオブジェクト指向プログラミングの基本的な知識が必要です。

## データストアの説明

4Dデータベースストラクチャーが [ORDA の前提条件](overview.md#orda-prerequisites) に準拠していれば、ORDA のデータストアは自動的にこれに基づいて機能します。

この例題では、以下の単純な 4Dデータベースストラクチャーを使用します:

![](../assets/en/ORDA/struc.png)

データストアとして公開されているものを知るために、新しいプロジェクトメソッドを作成し、以下の行を記述します:

```code4d
TRACE
```

このメソッドを実行すると、デバッガーウィンドウが呼び出されます。
式を挿入するために式エリアをダブルクリックし、`ds` と入力します。 これは、データストアオブジェクトを返します。
オブジェクトを展開すると、`ds` オブジェクトのプロパティとしてテーブルとフィールドが ORDA によって自動的に公開されることがわかります。

![](../assets/en/ORDA/debug1.png)

これはつまり、たとえば [Company]テーブルの cityフィールドを参照する必要がある場合、ORDA では次のように書くだけ事足ります:

```code4d
ds.Company.city // 都市の名前を返します
```

> ORDA の世界では，ds.Company は **データクラス** です。 ds.Company.city は **属性** です。

> ORDA は大文字と小文字を区別します。 `ds.company.city` が ds.Company.city 属性を参照することはありません。

また、ds.Company データクラスに `hires` プロパティが追加されていることにお気づきでしょうか。 これはフィールドに対応した属性ではありません。 `hires` は、実際には Company と Employee の間の _1対N_ リレーションの名前です:

![](../assets/en/ORDA/struc2s.png)
_Name of the relation as defined in the Inspector_

つまり、ある会社で働く従業員のリストにアクセスしたいとき、ORDA では次のように書きます:

```code4d
ds.Company.hires // 従業員のリストを返します
```

しかし、急ぎすぎてはいけません。 まずは、ORDA のデータクラスにデータを記録する方法を見ていきましょう。

## データの追加

ORDA では、`new()` コマンドを使ってデータクラスにレコードを追加することができます。

> ORDA の世界では、レコードは **エンティティ** であり、エンティティはそれ自体がオブジェクトです。 特定のオブジェクトに付随するコマンドを **メンバーメソッド** と呼びます。

```code4d
$entity:=ds.Company.new() // Company データクラスに
// 新しいエンティティ参照を作成し、  
// それを $entity 変数に代入します
```

新しいエンティティオブジェクトには、その親データクラスのすべての属性の "コピー" が含まれているので、それらに値を代入することができます:

```code4d
$entity.name:="ACME, inc."  
$entity.city:="London"  
// $entity.ID は自動的に設定されます
```

今のところ、エンティティはメモリ上にしか存在しません。 データファイルに保存するには、`save()` メンバーメソッドを使って保存する必要があります。

```code4d
$status:=$entity.save()
```
