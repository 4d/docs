---
id: parameters
title: 引数
---


メソッドや関数にデータを渡す必要がしばしば発生します。 これは引数によって容易にできます。

## 概要

**引数** (または **パラメーター**) とは、メソッドや関数が処理に必要とするデータのことです。 *引数* と *パラメーター* は厳密には違うものですが、このマニュアルでは同義語として使用されています。 引数は、ビルトインの 4Dコマンドにも渡されます。 以下の例は、“Hello” という文字列を引数としてビルトインの `ALERT` コマンドへ渡します:

```4d
ALERT("Hello")
```

メソッドやクラス関数に引数を渡す場合も同様におこないます。 たとえば、`getArea()` クラス関数が 2つの引数を受け取る場合、このクラス関数を呼び出すには以下のように書きます:

```
$area:=$o.getArea(50;100)
```

また、プロジェクトメソッド `DO SOMETHING` が3つの引数を受け取る場合、このメソッドを呼び出すには以下のように書きます:

```4d
DO_SOMETHING($WithThis;$AndThat;$ThisWay)
```

入力引数は、セミコロン (;) で区切ります。

メソッドを実行する専用コマンドを利用するときも、同じ原則で引数を渡します。

```4d
EXECUTE METHOD IN SUBFORM("Cal2";"SetCalendarDate";*;!05/05/20!)  
// サブフォーム "Cal2" のコンテキストにおいて // サブフォーム "Cal2" のコンテキストにおいて  SetCalendarDate を実行し
// その際に引数として日付リテラル !05/05/20! を渡します
```

メソッドやクラス関数からデータを **返す** こともできます。 以下は、文字列のデータ長を返すビルトインの `Length` コマンドを用いたステートメントです。 このステートメントでは、`Length` 関数が *MyLength* という変数に値を返します。  コードは以下のようになります:

```4d
MyLength:=Length("How did I get here?")
```

どのようなサブルーチンでも値を返すことができます。 各メソッドやクラス関数につき、定義できる戻り値は一つだけです。

入力および出力値は呼び出し時に [評価](#引数の渡し方-値か参照か) され、その値はそれぞれ自動的にサブルーチン (呼び出されたメソッドまたはクラス関数) 内のローカル変数に格納されます。 呼び出されるメソッドにおいて、これらのローカル変数を宣言するには次の 2つのシンタックスが利用できます:

- [名前付き変数](#名前付き引数) (ほとんどの場合に推奨)
- [受け渡し順に番号が付けられた変数](#順番引数) (順番引数)


引数の宣言にあたって、[名前付き](#名前付き引数) シンタックスと [順番](#順番引数) シンタックスは制限なく併用することができます。 例:

```4d
Function add($x : Integer)
    var $0;$2 : Integer
    $0:=$x+$2
```

### 初期化

引数は宣言されると、[**その型に対応するデフォルト値**](data-types.md#default-values) に初期化されます。 別の値が代入されない限り、セッション中はこの値が保持されます。



## 名前付き引数

呼び出されたメソッドやクラス関数において、引数の値はローカル変数に代入されます。 引数は **パラメーター名** とその **データ型** をコロン (:) で区切って宣言することができます。

- クラス関数の場合、引数は `Function` キーワードとともに宣言されます。
- メソッドの場合 (プロジェクトメソッド、フォームオブジェクトメソッド、データベースメソッド、トリガー)、引数はメソッドコード先頭の `#DECLARE` キーワードを使って宣言されます。

例:

```4d
Function getArea($width : Integer; $height : Integer) -> $area : Integer
```

```4d  
 // myProjectMethod
#DECLARE ($i : Integer) -> $myResult : Object
```


次のルールが適用されます:

- 宣言文はメソッドや関数のコードの先頭に位置していなければなりません。宣言文より前に置けるのはコメントと改行のみであり、それ以外の場合にはエラーが表示されます。
- 引数名は必ず `$` 文字で始まり、[プロパティ名の命名規則](identifiers.md#オブジェクトプロパティ) に準拠している必要があります。
- 複数のパラメーター (およびその型) を宣言する場合は、それらをセミコロン (;) で区切ります。
- 複数行シンタックスがサポートされています ("\\" 文字を使用)。


たとえば、`getArea()` 関数に 2つの引数を渡して呼び出す場合:

```4d
$area:=$o.getArea(50;100)
```

クラス関数において、引数の値はそれぞれ対応するパラメーターに代入されます:

```4d    
// クラス: Polygon
Function getArea($width : Integer; $height : Integer)-> $area : Integer
    $area:=$width*$height
```
> パラメーターの型が宣言されていない場合には、[`Variant`](dt_variant.md) 型として定義されます。

データベースメソッドを含むすべての 4Dメソッドにおいて `#DECLARE` キーワードの使用がサポートされています。 たとえば、`On Web Authentication` データベースメソッドにおいて、次のように名前付き引数を宣言できます:

```4d    
    // On Web Authentication データベースメソッド
#DECLARE ($url : Text; $header : Text; \
  $BrowserIP : Text; $ServerIP : Text; \
  $user : Text; $password : Text) \
  -> $RequestAccepted : Boolean
$entitySelection:=ds.User.query("login=:1"; $user)
// ハッシュパスワードを確認...
```

### 戻り値

関数の戻り値は、入力パラメーターリストに矢印 (->) を追加し、それに続けて宣言します。 例:

```4d
Function add($x : Variant; $y : Integer) -> $result : Integer
```

[名前付き引数シンタックス](#named-parameters) を利用している場合には、それらの引数は `#DECLARE` キーワードまたは `Function` プロトタイプによって自動的に宣言されます。 例:

```4d
Function add($x : Variant; $y : Integer): Integer
    $0:=$x+$y
```

:::warning

戻り値を含む引数の宣言をおこなうのは、一度だけです。 特に、同じ型であっても、同じ引数を入力と出力の両方として宣言することはできません。 例:

```qs
    // 無効な宣言
Function myTransform ($x : Integer) -> $x : Integer 
    // エラー: $x が2回宣言されています
```

:::

### サポートされているデータ型

名前付き引数の場合、[`var` キーワードでサポートされている](variables.md#var-キーワードによる宣言) データ型 (クラスオブジェクト含む) を使用できます。  例:

```4d
Function saveToFile($entity : cs.ShapesEntity; $file : 4D.File)
```





## 順番引数

[名前付き引数](#名前付き引数) シンタックスを使用するほかにも、引数は受け渡し順に番号が付けられた変数を使って宣言することができます: **$1**, **$2**, **$3**, ...。 ローカル変数の番号は、引数の順番を表わします。

> このシンタックスはクラス関数の場合もサポートされていますが、[名前付き引数](#名前付き引数) を使ったシンタックスの方が推奨されます。

たとえば、プロジェクトメソッド `DO SOMETHING` が3つの引数を受け取る場合、このメソッドを呼び出すには以下のように書きます:

```4d
DO_SOMETHING($WithThis;$AndThat;$ThisWay)
```

呼び出されるメソッドにおいて、それぞれの引数の値は自動的に、順に番号が付けられたローカル変数 ($1, $2, $3...) に格納されます:

```4d
  // DO_SOMETHING メソッド
  // すべての引数はテキスト型です
 C_TEXT($1;$2;$3)
 ALERT($1+" と "+$2+" と "+$3+" を受け取りました。 ")
  // $1 には $WithThis の値が代入されます
  // $2 には $AndThat の値が代入されます
  // 3 には $ThisWay の値が代入されます
```


### 戻り値

戻り値は自動的に、ローカル変数 `$0` に格納します。


たとえば、`Uppercase4` という以下のメソッドは、始めの 4文字を大文字に変換した文字列を返します:

```4d
// Uppercase4 メソッド
$0:=Uppercase(Substring($1;1;4))+Substring($1;5)
```

以下は、Uppercase4 をメソッドとして使用する例です:

```4d
$NewPhrase:=Uppercase4("This is good.")
```

変数 *$NewPhrase* には“THIS is good.” が格納されます。

戻り値 `$0` はサブルーチン内のローカル変数です。 したがって、サブルーチン内で通常のローカル変数のように使用できます。 たとえば:

```4d
// Do_something メソッド
$0:=Uppercase($1)
ALERT($0)
```

この例において、`$0` は大文字に変換した引数 `$1` の値を割り当てられ、その後 `ALERT` コマンドに引数として渡されました。 このように、サブルーチン内の他のローカル変数と同じように `$0` を使うことができます。 サブルーチン終了時に、その時点での `$0` の値を呼び出し元のメソッドに戻すのは 4Dがおこないます。


### サポートされているデータ型

順番引数には、あらゆる [式](quick-tour.md#式のタイプ) の形が使用できますが、例外があります:

- テーブル
- arrays

テーブルや配列の式は [ポインターを介した参照として](dt_pointer.md#メソッドの引数としてのポインター) 渡す必要があります。

## 引数の間接参照 (${N})

4Dプロジェクトメソッドは、可変個の引数を受け取ることができます。 `For...End for` ループや [`Count parameters`](https://doc.4d.com/4dv19/help/command/ja/page259.html) コマンド、**引数の間接参照シンタックス** を使って、これらの引数を扱うことができます。 メソッド内で、間接参照は `${N}` のように表示します。 ここの `N` は数値式です。 `${N}` を **ジェネリックパラメーター** (generic parameter) と呼びます。



### ジェネリックパラメーターの使い方

引数の数値を合計した結果を、引数として渡された表示形式で返すようなメソッドを考えてみましょう。 合計される数値の数は、メソッドが呼ばれるたびに変わります。 このメソッドでは数値と表示形式を引数としてメソッドに渡さなければなりません。

以下は `MySum` メソッドです:

```4d
 #DECLARE($format : Text) -> $result : Text
 $sum:=0
 For($i;2;Count parameters)
    $sum:=$sum+${$i}
 End for
 $result:=String($sum;$format)
```

このメソッドの引数は正しい順序で渡す必要があります。 最初に表示形式、次に可変個の数値引数です。

```4d
 Result:=MySum("##0.00";125,2;33,5;24) // "182.70"
 Result:=MySum("000";1;2;200) // "203"
```

メソッド内で 0、1、またはそれ以上のパラメーターを宣言した場合でも、任意の数の引数を渡すことができます。 呼び出されたメソッド内では、`${N}` シンタックスを使って引数を利用でき、可変長引数の型はデフォルトで [バリアント](dt_variant.md) です ([コンパイラー指示子](#ジェネリックパラメーターの宣言) を使ってこれらを宣言できます)。 [`Count parameters`](https://doc.4d.com/4dv19/help/command/ja/page259.html) コマンドを使用して、パラメーターが存在することをあらかじめ確認しておく必要があります。 例:

```4d
// foo メソッド
#DECLARE($p1: Text;$p2 : Text; $p3 : Date) 
For($i;1;Count parameters)
    ALERT("param "+String($i)+" = "+String(${$i}))
End for
```

このメソッドは次のように呼び出せます:

```4d
foo("hello";"world";!01/01/2021!;42;?12:00:00?) // 追加の引数が受け渡されます
```

> 引数の間接参照は以下の条件を守ることにより、正しく動作します: 引数の一部のみを間接参照する場合、直接参照する引数の後に間接参照引数を配置するようにします。


### ジェネリックパラメーターの宣言

他のローカル変数と同様、ジェネリックパラメーターはコンパイラーに指示する必要はありません。 ただし、曖昧さを回避するためには推奨されます。 宣言なしのジェネリックパラメーターは自動的に [Variant](dt_variant.md)型となります。

ジェネリックパラメーターの宣言には、コンパイラー指示子に ${N} を渡す、以下のシンタックスを使用します (N は 1つ目のジェネリックパラメーターの番号です):

```4d
 C_TEXT(${4})
```

> ジェネリックパラメーターの宣言は [受け渡し順](#順番引数) シンタックスでのみ使用できます。

このコマンドは、4番目以降に間接参照されるすべての引数のデータ型がテキストであることを意味します。 $1、$2、$3には、いかなるデータ型も使用できます。 しかしながら、$2を間接参照した場合には、間接参照の型宣言の影響を受けます。 このため、たとえば $2 が実数であっても、間接参照されればテキストと見なされます。

> 宣言に使用する数値は変数ではなく、定数でなくてはなりません。





## コンパイルモード用のパラメーター宣言

[インタープリターモード](interpreted.md) では必須ではないものの、問題を避けるにはメソッドや関数の各パラメーターを宣言しておくべきでしょう。

[名前付き引数シンタックス](#named-parameters) を利用している場合には、それらの引数は `#DECLARE` キーワードまたは `Function` プロトタイプによって自動的に宣言されます。 例:

```4d
Function add($x : Variant; $y : Integer)-> $result : Integer
    // すべての引数はデータ型とともに宣言されます
```


[順番引数シンタックス](#順番引数) を利用している場合には、引数がそれぞれ適切に宣言されていることを確認する必要があります。 次の例では `Capitalize` プロジェクトメソッドは第1パラメーターにテキスト型の引数を受け取り、戻り値としてテキスト型の値を返します:

```4d
  // Capitalize プロジェクトメソッド
  // Capitalize ( Text ) -> テキスト
  // Capitalize ( Source string ) -> 大文字の文字列

 C_TEXT($0;$1)
 $0:=Uppercase(Substring($1;1;1))+Lowercase(Substring($1;2))
```

`New process` コマンドなどでプロセスメソッドを呼び出す場合にも、そのメソッドが引数を受け取るのであれば、それらは明示的に宣言されていなくてはなりません。  例:

```4d
C_TEXT($string)
C_LONGINT($idProc;$int)
C_OBJECT($obj)

$idProc:=New process("foo_method";0;"foo_process";$string;$int;$obj)
```

"foo_method" において各パラメーターが適切に宣言されている場合のみ、コンパイルモードで上のコードを実行することができます:

```4d
//foo_method
C_TEXT($1)
C_LONGINT($2)
C_OBJECT($3)
...
```

> プロジェクトメソッドのパラメーター宣言は、コンパイルモード用にまとめて、"Compiler" で始まる名称の専用メソッドにておこなうことができます。 専用メソッド内で各メソッドのパラメーターをあらかじめ宣言する場合は、次のように書きます:
```4d  
 // Compiler_method
 C_REAL(OneMethodAmongOthers;$1) 
```
詳細については [インタープリターモードとコンパイルモード](interpreted.md) を参照ください。

パラメーターの宣言は次のコンテキストにおいても必須となります (これらのコンテキストは "Compiler" メソッドによる一括宣言をサポートしません)。

- データベースメソッド - たとえば、`On Web Connection データベースメソッド` は 6つのテキスト型の引数 $1 〜 $6 を受け取ります。 たとえすべての引数を使用しない場合でも、データベースメソッドの先頭で次のように宣言しなくてはなりません:

```4d
// On Web Connection
C_TEXT($1;$2;$3;$4;$5;$6)
```
- トリガー - トリガーの結果である $0 パラメーター (倍長整数) は、明確に定義されていなければコンパイラーによって型指定されます。 定義する場合は、トリガーの中でおこなう必要があります。

- `On Drag Over` フォームイベントを受け入れるフォームオブジェクト - `On Drag Over` フォームイベントの結果である $0 パラメーター (倍長整数) は、明確に定義されていなければコンパイラーが型を決定します。 定義する場合は、オブジェクトメソッドの中でおこなう必要があります。 **注:** コンパイラーは $0 を初期化しません。 したがって、`On Drag Over` フォームイベントを使用したら、直ちに $0 を初期化しなければなりません。 例:

```4d
 If($DataType=Is picture)
       $0:=-1
    End if
    ...
    If($DataType=Is picture)
       $0:=-1
    End if
    ... 
 End if
```

## 引数の型間違い

間違った型の引数を呼び出すことは、正しい実行を妨げる [エラー](error-handling.md) となります。 たとえば、次のようなメソッドを書いたとします:

```4d
// method1
#DECLARE($value : Text)
```

```4d
// method2
method1(42) // 型間違い。 期待されるのはテキスト
```

このケースは、コンテキストに応じて 4D で処理されます。

- [コンパイル済みプロジェクト](interpreted.md) では、可能な限りコンパイル時にエラーが生成されます。 それ以外の場合は、メソッドの呼び出し時にエラーが生成されます。
- インタープリタープロジェクトでは:
    + [名前付きシンタックス](#名前付き引数) (`#DECLARE` または `Function`) を使用して引数が宣言されている場合は、メソッドの呼び出し時にエラーが発生します。
    + [順番シンタックス](#順番引数) (`C_XXX`) を使用して宣言されている場合、エラーは発生せず、呼び出されたメソッドは期待される型の空の値を受け取ります。





## 入力 / 出力変数

これらの引数 ($1, $2...) はサブルーチン内で他のローカル変数と同様に使用できます。 しかしながら、引数として渡した変数の値を変更するコマンドをサブルーチン内で使用する場合 (例: `Find in field`)、$1, $2などを直接渡すことはできません。 まず標準のローカル変数等にコピーする必要があります (例: $myvar:=$1)。



## オブジェクトプロパティを名前付き引数として使用する

引数としてオブジェクトを渡すことによって **名前付き引数** を扱うことができます。 このプログラミング方法はシンプルかつ柔軟なだけでなく、コードの可読性も向上させます。

たとえば、`CreatePerson` メソッドを例にとると:

```4d
  // CreatePerson メソッド
 var $person : Object
 $person:=New object("Name";"Smith";"Age";40)
 ChangeAge($person)
 ALERT(String($person.Age))  
```

`ChangeAge` メソッドを次のように書けます:

```4d
  // ChangeAge メソッド
 var $1; $para : Object
 $para:=$1  
 $para.Age:=$para.Age+10
 ALERT($para.Name+" は "+String($para.Age)+" 歳です。 ")
```

これは [任意パラメーター](#任意パラメーター) を指定するにあたって非常に便利な方法です (後述参照)。 この場合、引数の不足は次のように対処できます:
- `Null` 値と比較することで、必要な引数がすべて提供されているかをチェックします
- 引数の値をプリセットします
- 渡されていない引数は空値として扱います

上述の `ChangeAge` メソッドの例では、Age およびName プロパティはどちらも必須であるため、引数オブジェクトに含まれていなければエラーが発生します。 これを避けるには、次のように記述することができます:

```4d

  // ChangeAge メソッド
 var $1; $para : Object
 $para:=$1  
 $para.Age:=Num($para.Age)+10
 ALERT(String($para.Name)+" は "+String($para.Age)+"歳です。 ")
```
すると、引数が不足してもエラーは生成されず、両方が欠落した場合の結果は " は 10歳です" となってしまうにせよ、いずれの引数も任意となります。

名前付き引数を利用すると、アプリケーションの保守やリファクタリングが簡単かつ安全におこなえます。 さきほどの例で、加算する年数を場合に応じて変えたほうが適切であると、あとから気づいたとします。 メソッドのパラメーターとして、加算年数を追加しなくてはなりません。 この場合、次のように書けます:

```4d
$person:=New object("Name";"Smith";"Age";40;"toAdd";10)
ChangeAge($person)

// ChangeAge メソッド
var $1;$para : Object
$para:=$1  
If ($para.toAdd=Null)
    $para.toAdd:=10
End if
$para.Age:=Num($para.Age)+$para.toAdd
ALERT(String($para.Name)+" は "+String($para.Age)+" 歳です。 ")
```

このように、既存のコードを変える必要はありません。 変更後のコードは変更前と同じように動作しますが、引数によって加算年数に数値を指定することもできるようになりました。

名前付き引数を使うと、すべてのパラメーターを任意にすることができます。 上の例ではすべてのパラメーターが任意で、いずれを指定してもよく、順序は考慮されません。



## 任意パラメーター

*4D ランゲージリファレンス* において、コマンドシンタックス中の { } 文字 (中括弧) はその引数が省略可能であることを示します。 たとえば、`ALERT (message{; okButtonTitle})` は *okButtonTitle* が省略できることを意味します。 この場合、次のような呼び出し方が可能です:

```4d
ALERT("Are you sure?";"Yes I am") // 2つの引数
ALERT("Time is over") // 1つの引数
```

プロジェクトメソッドも同様に、同じ型の引数であれば、右側に不定数の引数を受け取ることができます。 任意パラメーターの問題は、それらが指定されない場合への対処が必要だということです。 欠落がエラーに繋がってはいけません。 使用されなかったパラメーターにデフォルト値を代入するやり方が効果的です。

> 任意パラメーターが必要な場合、[オブジェクトプロパティを名前付き引数として使用する](#オブジェクトプロパティを名前付き引数として使用する) と型の制限がなく、柔軟で便利です。

`Count parameters` コマンドを使用すると、メソッドに渡された引数の数を確認することができるため、数に応じて異なる処理をおこなえます。

次の例はテキストメッセージを表示し、2つの引数が渡されていればディスク上のドキュメントに、3つ以上の場合は 4D Write Pro エリアにそのテキストを書き出します。

```4d
// APPEND TEXT プロジェクトメソッド
// APPEND TEXT ( テキスト { ; テキスト { ; オブジェクト } } )
// APPEND TEXT ( メッセージ { ; パス { ; 4DWPエリア } } )

 #DECLARE($message : Text; $path : Text; $wpArea : Object)

 ALERT($message)
 If(Count parameters>=3)
    WP SET TEXT($wpArea;$1;wk append)
 Else
    If(Count parameters>=2)
       TEXT TO DOCUMENT($path;$message)
    End if
 End if
```
このプロジェクトメソッドをアプリケーションに追加したあとは、次のように呼び出すことができます:

```4d  
APPEND TEXT(vtSomeText) // メッセージを表示します
APPEND TEXT(vtSomeText;$path) // メッセージを表示して、 $path のドキュメントに書き出します
APPEND TEXT(vtSomeText;"";$wpArea) // メッセージを表示して、 $wpArea の4D Write Pro ドキュメントに追記します
```




## 引数の渡し方: 値か参照か

引数を渡すとき、4D は呼び出し元メソッドのコンテキストにおいてその式を評価し、**結果の値** をクラス関数またはサブルーチンのローカル変数に格納します。 これらのローカル変数に格納されているのは、呼び出し元で使用されているフィールドや変数、式ではなく、渡された値のみです。 スコープがローカルに限られているため、クラス関数 / サブルーチン内でローカル変数の値を変えても、呼び出し元メソッドには影響ありません。 例:

```4d
    // MY_METHOD メソッド
DO_SOMETHING([People]Name) // [People]Name の値が "williams" だとします
ALERT([People]Name)

    // DO_SOMETHING メソッド
 $1:=Uppercase($1)
 ALERT($1)
```

`DO_SOMETHING` メソッドによって表示されたアラートボックスでは "WILLIAMS" と表示され、`MY_METHOD` メソッドによって表示されるアラートボックスでは "williams" と表示されます。 `DO_SOMETHING` メソッドは $1 の値をローカルな範囲で変更しましたが、これは `MY_METHOD` メソッドがサブルーチンに渡す引数として指定した [People]Last Name フィールドの値には影響しません。

もし `DO_SOMETHING` メソッド内でフィールドの値を変更したいのであれば、2通りのやり方があります:

1. サブルーチンに渡す式としてフィールドではなく、フィールドへのポインターを指定することができます。 この場合、以下のようにコードを書きます:

```4d
  // MY_METHOD メソッド
 DO_SOMETHING(->[People]Name) // [People]Name の値が "williams" だとします
 ALERT([People]Last Name)

  // DO_SOMETHING メソッド
 $1->:=Uppercase($1->)
 ALERT($1->)
```

この例では、引数として指定された式はフィールドではなく、フィールドへのポインターです。 そのため、`DO_SOMETHING` メソッド内において、$1 はフィールドの値ではなく、フィールドへのポインターになっています。 $1 引数によって **参照** される対象 (上記コード内での $1->) はフィールドそのものです。 その結果、参照されている対象を変更すると、その影響はサブルーチンのスコープを超え、実際のフィールドも変更されます。 さきほどの例題においては、両方のアラートボックスに "WILLIAMS" と表示されます。

2. `DO_SOMETHING` メソッドに "何かさせる" 代わりに、値を返すようにメソッドを書き直すこともできます。 たとえば、以下のようなコードです:

```4d
    // MY_METHOD メソッド
 [People]Name:=DO_SOMETHING([People]Name) // もとの [People]Name の値が "williams" だとします
 ALERT([People]Name)

    // DO_SOMETHING メソッド
 $0:=Uppercase($1)
 ALERT($0)
```

このようにサブルーチンの戻り値を使うことを "関数を使う" と言います。 詳細については [戻り値](#戻り値) の章を参照ください。


### 特殊ケース: オブジェクトやコレクションの場合

オブジェクトやコレクションのデータタイプは参照 (つまり、内部的な *ポインター*) を介した形でのみ扱われることに注意が必要です。

したがって、`$1、$2...` には *値* ではなく *参照* が格納されます。 `$1、$2...` の値をサブルーチン内で変更した場合、その変更は元となるオブジェクトやコレクションが使用されているところへと伝播します。 これは [ポインター](dt_pointer.md#メソッドの引数としてのポインター) に対する原理と同じものですが、`$1、$2...` の使用にあたって参照を外す必要はありません。

次の例では、`CreatePerson` メソッドはオブジェクトを作成したのち、それを引数として `ChangeAge` に渡します:

```4d
  // CreatePerson メソッド
 var $person : Object
 $person:=New object("Name";"Smith";"Age";40)
 ChangeAge($person)
 ALERT(String($person.Age))  
```

`ChangeAge` メソッドは受け取ったオブジェクトの Age 属性に 10を加えます:

```4d
  // ChangeAge メソッド
 #DECLARE ($person : Object)
 $person.Age:=$person.Age+10
 ALERT(String($person.Age))
```

`CreatePerson` メソッドを実行すると、サブルーチンにおいても同じオブジェクト参照が扱われているため、両方のアラートボックスにおいて ”50” と表示されます。

**4D Server:** "サーバー上で実行" オプションが使用された場合など、同じマシン上で実行されないメソッド間で引数が渡される場合、参照渡しは利用できません。 このような場合には、参照の代わりにオブジェクトとコレクションのコピーが引数として渡されます。


